//defines configurations with a global scope for all subprojects
subprojects {
	repositories {
	    // Use Maven Central for resolving dependencies.
	    mavenCentral()
	}
	
	apply plugin: 'java-library'
    apply plugin: 'eclipse'
	
	dependencies {
	    // Use JUnit Jupiter for testing.
	    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
	    
	    // JSR-330 dependency injection with Guice
	    api "com.google.inject:guice:5.0.1"
	    
	    modules {
	        module("javax.inject:javax.inject") {
	            replacedBy("jakarta.inject:jakarta.inject-api",
	            	 "replace guice javax.inject dependency with JPMS supported version")
	        }
    	}
    	
	    // javax.inject with JPMS support
		implementation 'jakarta.inject:jakarta.inject-api:1.0.3'
		
		implementation 'org.slf4j:slf4j-api:1.7.32'
	}
	
	tasks.named('test') {
    	// Use JUnit Platform for unit tests.
    	useJUnitPlatform()
	}
	
	ext {
		// define suffix for unique project names across all exaples (e.g when used in one Eclipse workspace)
		suffix = '-guice'
		// the runtime plugin directory
		pluginDir = "${project(':application-guice').buildDir}/libs/plugins"
	}
	
	// suppress compiler warnings about "requires transitive directive for an automatic module"
	// created by javax.inject and guice module usage
	compileJava {
		options.compilerArgs += '-Xlint:-requires-transitive-automatic'
	}
}
